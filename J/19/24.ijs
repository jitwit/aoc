load '~/code/aoc/aoc.ijs'
require 'viewmat'

world=:'#'=];._2 aoc 2019;24

rule =: ([*.=) +. ((-.@[)*.1 2 e.~ ])
lifeA =: rule [: +/ ((,-)=/~i.2)&(|.!.0)

#. |. , lifeA {: ([:~.],lifeA@:{:)^:_ ,: world

NB. graph for adjacent squares for part B
G0 =: (12~:i.25) # ([:< -.&_1 12)"1|:,/"_1((,-)=/~i.2)(|.!._1) i. 5 5
G0 =: ([: /:~ (- 12&<)) &.> G0
side =: 0 1 2 3 4 0 5 10 14 19 19 20 21 22 23 4 9 13 18 23
vu =: ,/7 11 16 12(,."0 1)_5]\side
G_1 =: ((([:<{:"1)/.~ {."1) vu) 7 11 16 12} 24$a:
G1 =: ((([:<{."1)/.~ {:"1) vu) (~.side)} 24$a:
C0 =: {{ +/@:{&y &> G0 }}
C1 =: {{ +/@:{&y &> G1 }}
C_1 =: {{ +/@:{&y &> G_1 }}

clean =: }:^:(1-+./@{:) @ (}.^:(1-+./@{.))
pad =: 0&, @ ,&0
lifeB =. [: clean [: (rule C0"1+(C1"1@:(1&|.))+(C_1"1@:(_1&|.))) pad

+/,lifeB^:200 ,: (12~:i.25) #,world

(optimize-level 3)
(load "~/code/aoc/load.ss")
(advent-year 20) (advent-day 19)

(define input
  (parse-advent lines-raw))

(define (parse-production S)
  (if (string-contains S "\"")
      (list (string-ref S 1))
      (map string->number (string-tokenize S))))

(define (parse-rule line colon)
  `(,(string->number (substring line 0 colon))
    ,@(map parse-production
	   (string-tokenize (substring line (+ colon 2) (string-length line))
			    (char-set-complement (char-set #\|))))))

(define (alist->ht alist)
  (define ht (make-hash-table))
  (for-each (lambda (x.y)
	      (hashtable-set! ht (car x.y) (cdr x.y)))
	    alist)
  ht)

(define (matches? S rule-set)
  (define mem (make-hashtable equal-hash equal?))
  (define (g lo hi rule)
    (if (hashtable-contains? mem (list lo hi rule))
	(hashtable-ref mem (list lo hi rule) 'ohno)
	(let ((result (ormap (lambda (production)
			       (match production
				 ((a)
				  (if (char? a)
				      (and (= lo (1- hi)) (eqv? a (string-ref S lo)))
				      (g lo hi a)))
				 ((a b)
				  (let lp ((i (1+ lo)))
				    (and (< i hi)
					 (or (and (g lo i a)
						  (g i hi b))
					     (lp (1+ i))))))
				 ((a b c)
				  (let lp1 ((i (1+ lo)))
				    (and (< i (1- hi))
					 (let lp2 ((j (1+ i)))
					   (if (>= j hi)
					       (lp1 (1+ i))
					       (or (and (g lo i a)
							(g i j b)
							(g j hi c))
						   (lp2 (1+ j))))))))
				 (else #f)))
			     (hashtable-ref rule-set rule '()))))
	  (hashtable-set! mem (list lo hi rule) result)
	  result)))
  (g 0 (string-length S) 0))

(define (do-matching strings rules)
  (length (filter (lambda (s)
		    (matches? s rules))
		  strings)))

(define (solve)
  (define rules
    (alist->ht
     (filter-map (lambda (line)
		   (let ((j (string-contains line ":")))
		     (and j (parse-rule line j))))
		 input)))
  (define strings
    (filter (lambda (line)
	      (not (or (zero? (string-length line)) (string-contains line ":"))))
	    input))
  (display-ln (do-matching strings rules))
  (hashtable-set! rules 8 '((42) (42 8)))
  (hashtable-set! rules 11 '((42 31) (42 11 31)))
  (display-ln (do-matching strings rules)))
